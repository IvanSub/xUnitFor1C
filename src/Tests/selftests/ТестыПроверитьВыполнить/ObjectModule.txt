Перем ЮТест;

Функция ПолучитьСписокТестов(ОбъектТестирования) Экспорт
	
	юТест = ОбъектТестирования;
	
	ВсеТесты = Новый Массив;

	ВсеТесты.Добавить("Тесты_ПроверитьВыполнилось_БезПараметров");
	ВсеТесты.Добавить("Тесты_ПроверитьВыполнилось_ПараметрыВСтруктуре");
	ВсеТесты.Добавить("Тесты_ПроверитьВыполнилось_ПараметрыВМассиве");
	ВсеТесты.Добавить("Тесты_ПроверитьВыполнилосьУпал");
	ВсеТесты.Добавить("Тест_ПроверитьНеВыполнилось_БезПараметров");
	ВсеТесты.Добавить("Тест_ПроверитьНеВыполнилось_ЯвноУказанНеопределенныйПараметр");
	ВсеТесты.Добавить("Тест_ПроверитьНеВыполнилось_ПараметрыВСтруктуре");
	ВсеТесты.Добавить("Тест_ПроверитьНеВыполнилось_ПараметрыВМассиве");
	ВсеТесты.Добавить("Тест_ПроверитьНеВыполнилосьУпал");
	ВсеТесты.Добавить("Тесты_ПроверитьМетодВыполнился_БезПараметров");
	ВсеТесты.Добавить("Тесты_ПроверитьМетодВыполнился_ЯвноУказанНеопределенныйПараметр");
	ВсеТесты.Добавить("Тесты_ПроверитьМетодВыполнился_Параметры");
	ВсеТесты.Добавить("Тесты_ПроверитьМетодВыполнилсяУпал");
	ВсеТесты.Добавить("Тесты_ПроверитьМетодНеВыполнился");
	ВсеТесты.Добавить("Тесты_ПроверитьМетодНеВыполнилсяУпал");

	Возврат ВсеТесты;
	
КонецФункции

Процедура Тесты_ПроверитьВыполнилось_БезПараметров() Экспорт
	рез = 2;
	ЮТест.ПроверитьВыполнилось("рез = 10");
	ЮТест.ПроверитьРавенство(2, рез, "рез");
КонецПроцедуры

Процедура Тесты_ПроверитьВыполнилось_ПараметрыВСтруктуре() Экспорт
	рез = 2;
	Параметры = Новый Структура("рез", рез);
	ЮТест.ПроверитьВыполнилось("Параметры.рез = 10", Параметры);
	ЮТест.ПроверитьРавенство(10, Параметры.рез, "Параметры.рез");
КонецПроцедуры

Процедура Тесты_ПроверитьВыполнилось_ПараметрыВМассиве() Экспорт
	рез = 2;
	Параметры = Новый Массив;
	Параметры.Добавить(рез);
	ЮТест.ПроверитьВыполнилось("Параметры[0] = 10", Параметры);
	ЮТест.ПроверитьРавенство(2, рез, "рез");
	ЮТест.ПроверитьРавенство(10, Параметры[0], "Параметры[0]");
КонецПроцедуры

Процедура Тесты_ПроверитьВыполнилосьУпал() Экспорт
	рез = 2;
	ЮТест.ПроверитьНеВыполнилось("ЭтотОбъект.ПроверитьВыполнилось(""рез = 1/0"")", "Хотели, чтобы алгоритм ""рез = 1/0"" выполнился, а он упал с ошибкой ""{(1)}: Деление на 0");
КонецПроцедуры

Процедура Тест_ПроверитьНеВыполнилось_БезПараметров() Экспорт
	рез = 2;
	ЮТест.ПроверитьНеВыполнилось("рез = 1/0", "Деление на 0");
КонецПроцедуры

Процедура Тест_ПроверитьНеВыполнилось_ЯвноУказанНеопределенныйПараметр() Экспорт
	рез = 2;
	ЮТест.ПроверитьНеВыполнилось("рез = 1/0", Неопределено,"Деление на 0");
КонецПроцедуры

Процедура Тест_ПроверитьНеВыполнилось_ПараметрыВСтруктуре() Экспорт
	рез = 2;
	Параметры = Новый Структура("рез", рез);
	ЮТест.ПроверитьНеВыполнилось("Параметры.рез = 1/0", Параметры, "Деление на 0");
	ЮТест.ПроверитьРавенство(2, рез, "рез");
	ЮТест.ПроверитьРавенство(2, Параметры.рез, "Параметры.рез");
КонецПроцедуры

Процедура Тест_ПроверитьНеВыполнилось_ПараметрыВМассиве() Экспорт
	рез = 2;
	Параметры = Новый Массив;
	Параметры.Добавить(рез);
	ЮТест.ПроверитьНеВыполнилось("Параметры[0] = 1/0", Параметры, "Деление на 0");
	ЮТест.ПроверитьРавенство(2, рез, "рез");
	ЮТест.ПроверитьРавенство(2, Параметры[0], "Параметры[0]");
КонецПроцедуры

Процедура Тест_ПроверитьНеВыполнилосьУпал() Экспорт
	ЮТест.ПроверитьНеВыполнилось("ЭтотОбъект.ПроверитьНеВыполнилось(""рез = 10"", ""Деление на 0"")", "Хотели, чтобы алгоритм ""рез = 10"" упал, а он выполнился");
	ЮТест.ПроверитьНеВыполнилось("ЭтотОбъект.ПроверитьНеВыполнилось(""рез = 1/0"", ""Другое сообщение об ошибке"")", "Хотели, чтобы алгоритм ""рез = 1/0"" упал с сообщением об ошибке ""Другое сообщение об ошибке"", а он упал с сообщением ""{(1)}: Деление на 0");
КонецПроцедуры

Процедура Тесты_ПроверитьМетодВыполнился_БезПараметров() Экспорт
	Текст = Новый ТекстовыйДокумент;
	Текст.УстановитьТекст("ывадол");
	ЮТест.ПроверитьМетодВыполнился(Текст, "Очистить");
	ЮТест.ПроверитьНеЗаполненность(Текст.ПолучитьТекст(), "Текст");
КонецПроцедуры

Процедура Тесты_ПроверитьМетодВыполнился_ЯвноУказанНеопределенныйПараметр() Экспорт
	Текст = Новый ТекстовыйДокумент;
	ЮТест.ПроверитьМетодВыполнился(Текст, "Очистить", Неопределено, "Текст.Очистить");
	ЮТест.ПроверитьНеЗаполненность(Текст.ПолучитьТекст(), "Текст");
КонецПроцедуры

Процедура Тесты_ПроверитьМетодВыполнился_Параметры() Экспорт
	Текст = Новый ТекстовыйДокумент;
	Параметры = Новый Массив;
	Параметры.Добавить("НовыйТекст");
	ЮТест.ПроверитьМетодВыполнился(Текст, "УстановитьТекст", Параметры);
	ЮТест.ПроверитьРавенство("НовыйТекст", Текст.ПолучитьТекст(), "Текст.ПолучитьТекст()");
КонецПроцедуры

Процедура Тесты_ПроверитьМетодВыполнилсяУпал() Экспорт
	ЮТест.ПроверитьНеВыполнилось("Текст = Новый ТекстовыйДокумент; ЭтотОбъект.ПроверитьМетодВыполнился(Текст, ""__Очистить"")", "Хотели, чтобы код ""Объект.__Очистить()"" выполнился, а он упал с ошибкой ""{(1)}: Метод объекта не обнаружен (__Очистить)");
КонецПроцедуры

Процедура Тесты_ПроверитьМетодНеВыполнился() Экспорт
	Текст = Новый ТекстовыйДокумент;
	ЮТест.ПроверитьМетодНеВыполнился(Текст, "__Очистить", "Метод объекта не обнаружен (__Очистить)");
КонецПроцедуры

Процедура Тесты_ПроверитьМетодНеВыполнилсяУпал() Экспорт
	ЮТест.ПроверитьНеВыполнилось("Текст = Новый ТекстовыйДокумент;ЭтотОбъект.ПроверитьМетодНеВыполнился(Текст, ""Очистить"", ""Тут неважно, что"")", "Хотели, чтобы код ""Объект.Очистить()"" упал, а он выполнился");
	ЮТест.ПроверитьНеВыполнилось("Текст = Новый ТекстовыйДокумент;ЭтотОбъект.ПроверитьМетодНеВыполнился(Текст, ""__Очистить"", ""Тут неважно, что"")", "Хотели, чтобы код ""Объект.__Очистить()"" упал с сообщением об ошибке ""Тут неважно, что"", а он упал с сообщением ""{(1)}: Метод объекта не обнаружен (__Очистить)");
КонецПроцедуры

Процедура Bug_4456() Экспорт
	ЮТест.ПроверитьВыполнилось("Упало = Истина");
	ЮТест.ПроверитьВыполнилось("Успех = Ложь");
КонецПроцедуры